(use srfi-1)
(use srfi-13)
(use file.util)
(use gauche.parseopt)
(use gauche.process)
(use simply)

(require "@NMK_PATH@/src/common")
(require "@NMK_PATH@/src/words")
;(require "@NMK_PATH@/src/scanner")
(require "@NMK_PATH@/src/parser")

(define *library-path* "@NMK_PATH@/lib")
(define *scanner-path* "@NMK_PATH@/src/scanner/nmk-scan")

; =init
; -----------------------
(define (namakemono-initialize)
  ; default words
  (for-each
    (lambda (x)
      (set-variable (car x) (cadr x))
      )
    *words*)

  ; library
  (for-each load-source (collect-library *library-path*))
  )

; =del-tab
; --------------
(define (del-tab str)
  (regexp-replace-all* str #/[\t]/ "")
  )

; =run-tokens
; ------------------
(define (run-tokens all-tokens)
  (debug 4 "* all tokens\n  " all-tokens)
  (fold
    (lambda (each-line-token last-result)
      (let1 each-pipe-token (list-split each-line-token (lambda (x) (eq? (car x) :pipe)))
        (cond
          [(! null? each-pipe-token)
           (debug 4 "* each pipe token\n  " each-pipe-token)
           (let1 res (parser each-pipe-token)
             (debug 4 "")
             res
             )
           ]
          [else last-result]
          )
        )
      )
    '()
    (list-split all-tokens (lambda (x) (eq? (car x) :end)))
    )
  )

; =collect-library
; --------------------
(define (collect-library path)
  (fold
    (lambda (file res)
      (cond
        [(file-is-directory? file)
         (append res (collect-library file))
         ]
        [else
          (if (#/.+\.nmk$/ file)
            (append res (list file))
            res
            )
          ]
        )
      )
    '()
    (directory-list path :children? #t :add-path? #t))
  )

; =scanner
; ----------------
(define (scanner source-code)
  (call-with-process-io
    *scanner-path*
    (lambda (in out)
      (display source-code out)
      (close-output-port out)
      (read in)
      ))
  )

; =run-source
; --------------------
(define (run-source source-code)
  (let1 all-tokens (scanner source-code)
    (run-tokens (collect-lambda all-tokens))
    )
  )

; =load-source
; ----------------
(define (load-source filename)
  (let1 code (call-with-input-process #`",|*scanner-path*| ,|filename|" read)
    (run-tokens (collect-lambda code))
    )
  )

; =command-line-environment
(define (command-line-environment)
  (define (get-user-input)
    (display "nmk> ")
    (flush)
    (read-line)
    )

  (let loop((input (get-user-input)))
    (cond
      [(or (! string? input) (string=? input "exit"))
       (print "bye")
       ]
      [(! string=? input "")
       (let1 res (guard (e (else e))
                   (run-source input)
                   )
         (print (if (eq? (class-of res) <error>) (slot-ref res 'message) res))
         (loop (get-user-input))
         )
       ]
      [else
        (loop (get-user-input))
        ]
      )
    )
  )

; =version-print
; -----------------
(define (version-print)
  (print "namakemono " *nmk-version*)
  )

; =main
; ---------------
(define (main args)
  (let-args (cdr args)
    ((debug "d|debug=i" 0)
     (version "v|version")
     . rest-args
     )

    ; initialize
    (namakemono-initialize)

    (set! *debug* debug)

    (let1 stdin (standard-input-port)
      (cond
        [(char-ready? stdin)
         ; 標準入力でソースが読まれた場合
         (run-source (port->string stdin))
         ]
        [else
          ; ファイルの実行など
          (case (length rest-args)
            ; command line mode
            [(0)
             (when version
               (version-print)
               (exit)
               )
             (command-line-environment)
             ]

            ; file execute mode
            [else
              ; command-line parameter
              (set-variable :*args* (cdr rest-args))
              ; load and run source
              (load-source (car rest-args))
              ]
            )
          ]
        )
      )
    )
  0
  )

